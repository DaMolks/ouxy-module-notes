plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

android {
    namespace 'com.damolks.ouxy.modules.notes'
    compileSdk 34

    defaultConfig {
        minSdk 24
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

task createModuleJar(type: Jar) {
    dependsOn 'assembleRelease'
    
    // S'assure que le dossier releases existe
    doFirst {
        def releasesDir = new File("${rootDir}/releases")
        releasesDir.mkdirs()
        println "Releases directory: ${releasesDir.absolutePath}"
    }

    from("${buildDir}/intermediates/javac/release/classes")
    from("${buildDir}/tmp/kotlin-classes/release")
    from(android.sourceSets.main.resources.srcDirs)
    
    exclude 'BuildConfig.class'
    exclude 'R.class'
    exclude 'R$*.class'
    
    destinationDirectory = file("${rootDir}/releases")
    archiveFileName = 'notes-module.jar'

    // Log le chemin du JAR créé
    doLast {
        println "\nModule JAR created at: ${archiveFile.get().asFile.absolutePath}"
    }
}

dependencies {
    implementation files('libs/ouxy-sdk.jar')
    
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
}