plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.9.22"
    }
}

android {
    namespace 'com.damolks.ouxy.modules.notes'
    compileSdk 34

    defaultConfig {
        minSdk 24
        multiDexEnabled true
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

configurations {
    jarInclude
    jarInclude.extendsFrom implementation
}

task extractDependencies(type: Sync) {
    from configurations.jarInclude
    into layout.buildDirectory.dir('tmp/jar-deps')
}

task createIntermediateJar(type: Jar) {
    dependsOn 'assembleRelease', extractDependencies
    archiveFileName = 'intermediate.jar'
    destinationDirectory = layout.buildDirectory.dir('intermediates/tmp')
    
    // Include compiled classes
    from(layout.buildDirectory.dir('intermediates/javac/release/classes'))
    from(layout.buildDirectory.dir('tmp/kotlin-classes/release'))
    
    // Include dependency classes
    from(fileTree(layout.buildDirectory.dir('tmp/jar-deps'))) {
        include '**/*.class'
        include '**/*.kotlin_metadata'
        include '**/*.kotlin_module'
        exclude 'META-INF/maven/**'
        exclude 'META-INF/proguard/**'
        exclude 'META-INF/versions/**'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
        exclude 'META-INF/MANIFEST.MF'
    }
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    doFirst {
        destinationDirectory.get().asFile.mkdirs()
    }
}

task createDex(type: Exec) {
    dependsOn createIntermediateJar
    workingDir layout.buildDirectory.get().asFile
    
    def androidHome = System.getenv("ANDROID_HOME")
    def buildToolsVersion = android.buildToolsVersion
    def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
    def d8Executable = isWindows ? "d8.bat" : "d8"
    def dxPath = new File(androidHome, "build-tools/${buildToolsVersion}/${d8Executable}")
    
    def intermediateJar = layout.buildDirectory.file('intermediates/tmp/intermediate.jar').get().asFile
    def dexOutputJar = layout.buildDirectory.file('intermediates/dex/release.jar').get().asFile
    
    def platformDir = new File(androidHome, "platforms")
    def androidPlatform = platformDir.listFiles().find { it.name.startsWith("android-") }
    def androidJar = androidPlatform ? new File(androidPlatform, "android.jar") : null
    
    doFirst {
        dexOutputJar.parentFile.mkdirs()
        if (!androidJar?.exists()) {
            throw new GradleException("Cannot find android.jar in any of the platform directories in ${platformDir.absolutePath}")
        }
    }
    
    commandLine dxPath.absolutePath,
            "--lib",
            androidJar.absolutePath,
            "--output",
            dexOutputJar.absolutePath,
            intermediateJar.absolutePath
}

task createModuleJar(type: Jar) {
    dependsOn createDex
    
    archiveFileName = 'notes-module.jar'
    destinationDirectory = layout.projectDirectory.dir('releases')
    
    from(zipTree(layout.buildDirectory.file('intermediates/dex/release.jar')))
    from(android.sourceSets.main.resources.srcDirs)
    
    doFirst {
        destinationDirectory.get().asFile.mkdirs()
    }
    
    doLast {
        println "Module JAR created at: ${archiveFile.get().asFile.absolutePath}"
        // Print jar content for debugging
        def jarFile = new java.util.jar.JarFile(archiveFile.get().asFile)
        jarFile.entries().each { entry ->
            println "${entry.name} (${entry.size} bytes)"
        }
        jarFile.close()
    }
}

dependencies {
    implementation files('libs/ouxy-sdk.jar')
    
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    
    jarInclude 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.2'
    jarInclude 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    jarInclude "org.jetbrains.kotlin:kotlin-stdlib:1.9.22"
}